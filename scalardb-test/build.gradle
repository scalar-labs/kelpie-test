plugins {
    id 'java-library-distribution'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'com.palantir.docker' version '0.25.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    jcenter()
}

def awssdkVersion = "2.17.69"
def dockerVersion = project.hasProperty('dockerVersion') ? project.dockerVersion : 'latest'

dependencies {
    implementation group: 'com.scalar-labs', name: 'kelpie', version: '1.2.1'
    implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    implementation group: 'io.github.resilience4j', name: 'resilience4j-retry', version: '1.3.1'

    if (project.hasProperty("scalardb-libs-path")) {
        implementation fileTree(project.property("scalardb-libs-path")) { include '*.jar' }
    } else {
        implementation group: 'com.scalar-labs', name: 'scalardb', version: '4.0.0-SNAPSHOT'
        implementation group: 'com.google.guava', name: 'guava', version: '30.1-jre'
        implementation group: 'com.azure', name: 'azure-cosmos', version: '4.20.0'
        implementation group: 'software.amazon.awssdk', name: 'dynamodb', version: "${awssdkVersion}"
        implementation group: 'software.amazon.awssdk', name: 'core', version: "${awssdkVersion}"
        implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.8.0'
    }
}

shadowJar {
    mergeServiceFiles()
}

docker {
    name "ghcr.io/scalar-labs/kelpie-test-scalardb:${dockerVersion}"
    files tasks.shadowJar.outputs
    copySpec.with {
        from('.') {
            include 'build/classes/**'
            include 'grpc-config.toml.tmpl'
            into '.'
        }
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
